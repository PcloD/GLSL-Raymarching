<!DOCTYPE HTML> 
<html> 
	<head> 
		<meta charset="utf-8"> 
		<meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">
		<title>WebGL Boilerplate</title> 
		
		<style> 
			body {
				background-color: #000000;
				margin: 0px;
				overflow: hidden;
			}
		</style> 
	</head> 
	<body> 
 
		<canvas></canvas>
		<div id="info"></div> 
 
		<script id="vs" type="x-shader/vertex"> 
 
			attribute vec3 position;
 
			void main() {
 
				gl_Position = vec4( position, 1.0 );
 
			}
 
		</script> 
 
		<script id="fs" type="x-shader/fragment"> 
 
#ifdef GL_ES
precision highp float;
#endif

uniform float time;
#define time time * 30.0
uniform vec2 resolution;
uniform vec4 mouse;
uniform sampler2D tex0;
uniform sampler2D tex1;

#define MAX_STEPS 200

#define shadowColor vec3(0.0,0.3,0.7)
#define buildingsColor vec3(1.0,1.0,1.0)
#define groundColor vec3(1.0,1.0,1.0)
#define redColor vec3(1.0,0.1,0.1)
#define skyColor vec3(0.9,1.0,1.0)
#define viewMatrix mat4(0.0)
#define fovyCoefficient 1.0
#define shadowHardness 7.0

#define epsilon 0.01
#define PI 3.14159265

#define NO_HIT 0
#define HAS_HIT 1
// materials
#define SKY_MTL 0
#define GROUND_MTL 1
#define BUILDINGS_MTL 2
#define RED_MTL 3


float PlaneDistance(in vec3 point, in vec3 normal, in float pDistance)
{
return dot(point - (normal * pDistance), normal);
}

float SphereDistance(vec3 point, vec3 center, float radius)
{
  point.z = mod(point.z+15.0, 230.0)-15.0;
  point.x = mod(point.x+15.0, 230.0)-15.0;
  //point.y = mod(point.y, 30.0);
  return length(point - center) - radius;
}


float CubeDistance2 (in vec3 point, in vec3 size) {

  return length(max(abs(point)-size, 0.0));
}

vec3 DistanceRepetition(in vec3 point, in vec3 repetition ) {
  vec3 q = mod(point, repetition)-0.5*repetition;
  return q;
}


float CubeRepetition(in vec3 point, in vec3 repetition ) {
    vec3 q = mod(point, repetition)-0.5*repetition;
    q.y = point.y;
    return CubeDistance2 ( q, vec3 (2.0, 4.0, 2.0));
}

void applyFog( in float distance, inout vec3 rgb ){

    float fogAmount = (1.0 - clamp(distance*0.0015,0.0,1.0) );
    //float fogAmount = exp( -distance* 0.006 );
    vec3 fogColor = vec3(0.9,0.95,1);
    //if( fogAmount < 0.6 ) 
    //  rgb = vec3(1.0,1.0,0.0);
    //else
    //rgb = clamp( rgb, 0.0, 1.0);
    rgb = mix( skyColor, rgb, fogAmount );
}


float RedDistance(in vec3 position)
{
    return SphereDistance(position, vec3(0.0, 3.0, 5.0), 5.0);
}

float BuildingsDistance(in vec3 position)
{
    return min(
      CubeRepetition(position, vec3(17.0, 0.0, 20.0))
      , CubeRepetition(position+vec3(350.0,-2.0,0.0), vec3(23.0, 0.0, 23.0))
    );
}

float GroundDistance(in vec3 position)
{
    return PlaneDistance(position, vec3(0.0,1.0,0.0), 0.0);
}

float DistanceField(in vec3 position, out int mtl )
{
    float redDistance = RedDistance(position);
    float bldDistance = BuildingsDistance(position);
    float gndDistance = GroundDistance(position);
    float closest = gndDistance;
    mtl = GROUND_MTL;
    if ( bldDistance < closest )
    {
        closest = bldDistance;
        mtl = BUILDINGS_MTL;
    }
    if ( redDistance < closest )
    {
        closest = redDistance;
        mtl = RED_MTL;
    }
    return closest;
}


float Softshadow( in vec3 landPoint, in vec3 lightVector, float mint, float maxt, float iterations )
{
    float penumbraFactor = 1.0;
    vec3 sphereNormal;
    float t = mint; //(mint + rand(gl_FragCoord.xy) * 0.01);
    for( int s = 0; s < 100; ++s )
    {
        if(t > maxt) break;
        float nextDist = min(
            BuildingsDistance(landPoint + lightVector * t )
            , RedDistance(landPoint + lightVector * t )
        );

        if( nextDist < 0.001 ){
            return 0.0;
        }
        //float penuAttenuation = mix (1.0, 0.0, t/maxt);
        penumbraFactor = min( penumbraFactor, iterations * nextDist / t );
        t += nextDist;
    }
    return penumbraFactor;
}


vec3 RayMarch(in vec3 position, in vec3 direction, out int mtl)
{
    float nextDistance = 1.0;
    for (int i = 0; i < MAX_STEPS ; ++i)
    {
        nextDistance = DistanceField(position,mtl);
        
        if ( nextDistance < 0.01)
        {
            return position;
        }
        position += direction * nextDistance;
    }
    // out of steps
    if (direction.y < 0.0 )
    {
        mtl = GROUND_MTL;
    }
    else
    {
        mtl = SKY_MTL;
    }
    return position;
}

vec3 MaterialColor( int mtl )
{
    if(mtl==SKY_MTL) return skyColor;
    if(mtl==BUILDINGS_MTL) return buildingsColor;
    if(mtl==GROUND_MTL) return groundColor;
    if(mtl==RED_MTL) return redColor;

    return vec3(1.0,0.0,1.0); // means error
}

vec3 ComputeNormal(vec3 pos, int material)
{
    int dummy;
    return normalize(
        vec3(
          DistanceField( vec3(pos.x + epsilon, pos.y, pos.z), dummy ) - DistanceField( vec3(pos.x - epsilon, pos.y, pos.z), dummy )
        , DistanceField( vec3(pos.x, pos.y + epsilon, pos.z), dummy ) - DistanceField( vec3(pos.x, pos.y - epsilon, pos.z), dummy )
        , DistanceField( vec3(pos.x, pos.y, pos.z + epsilon), dummy ) - DistanceField( vec3(pos.x, pos.y, pos.z - epsilon), dummy )
        )
    );
}

void FishEyeCamera( vec2 screenPos, float ratio, float fovy, mat4 transform, out vec3 position, out vec3 direction )
{
    screenPos.y -= 0.2;
    screenPos *= vec2(PI*0.5,PI*0.5/ratio)/fovy;
    
    direction = vec3(
           sin(screenPos.y+PI*0.5)*sin(screenPos.x)
        , -cos(screenPos.y+PI*0.5)
        , sin(screenPos.y+PI*0.5)*cos(screenPos.x)
    );
    position = vec3(5.0*sin(time*0.01), 15.0, time);
}


float AmbientOcclusion (vec3 point, vec3 normal, float stepDistance, float samples) 
{
  float occlusion = 1.0;
  int tempMaterial;
  
  for (int i = 0; i < 20; ++i ) 
  {
    if(--samples < 0.0) break;
    occlusion -= (samples * stepDistance - (DistanceField( point + normal * samples * stepDistance, tempMaterial))) / pow(2.0, samples);
  }
  return occlusion;
}

void main(void)
{
    float ratio = resolution.x / resolution.y;
    // position on the screen
    vec2 screenPos;
    screenPos.x = (gl_FragCoord.x/resolution.x - 0.5);
    screenPos.y = gl_FragCoord.y/resolution.y - 0.5;

    vec3 direction;
    vec3 position;
    FishEyeCamera(screenPos, ratio, fovyCoefficient, viewMatrix, position, direction);
    int material;
    vec3 hitPosition = RayMarch(position, direction, material);

    if( material == SKY_MTL && direction.y < 0.0 )
    {
       material = GROUND_MTL;
       gl_FragColor = vec4(1.0,0.0,0.0,1.0);
    }


    vec3 hitColor;
    if( material != SKY_MTL ) // has hit something
    {
        vec3 lightpos = vec3(50.0 * sin(time*0.01), 10.0 + 40.0 * abs(cos(time*0.01)), (time) + 100.0 );
        vec3 lightVector = normalize(lightpos - hitPosition);
        // soft shadows
        float shadow = Softshadow(hitPosition, lightVector, 0.1, 50.0, shadowHardness);
        // attenuation due to facing (or not) the light
        vec3 normal = ComputeNormal(hitPosition, material);
        float attenuation = clamp(dot(normal, lightVector),0.0,1.0)*0.6 + 0.4;
        shadow = min(shadow, attenuation);
        //material color
        vec3 mtlColor = MaterialColor(material);
        
        if(material == BUILDINGS_MTL){
          mtlColor = mix(shadowColor, mtlColor, clamp(hitPosition.y/7.0, 0.0, 1.0));
        }
        hitColor = mix(shadowColor, mtlColor, 0.4+shadow*0.6);
        vec3 hitNormal = ComputeNormal(hitPosition, 0);
        float AO = AmbientOcclusion(hitPosition, hitNormal, 0.35, 5.0);
        hitColor = mix(shadowColor, hitColor, AO);
        
        applyFog( length(position-hitPosition), hitColor);
        gl_FragColor = vec4(hitColor, 1.0);
    }
    else // sky
    {
        float shade = direction.y;
        vec3 hitColor = mix(skyColor, vec3(0.3,0.3,0.7), shade);
        gl_FragColor = vec4(hitColor, 1.0);
    }

}
 
		</script> 
 
		<script> 
 
			/**
			 * Provides requestAnimationFrame in a cross browser way.
			 * paulirish.com/2011/requestanimationframe-for-smart-animating/
			 */
			window.requestAnimationFrame = window.requestAnimationFrame || ( function() {

				return  window.webkitRequestAnimationFrame ||
				        window.mozRequestAnimationFrame ||
				        window.oRequestAnimationFrame ||
				        window.msRequestAnimationFrame ||
				        function(  callback, element ) {
					        window.setTimeout( callback, 1000 / 60 );
				        };

			})();

 
			var canvas, 
			    gl, 
			    buffer, 
			    vertex_shader, fragment_shader, 
			    currentProgram,
			    vertex_position, 
			    parameters = {  start_time  : new Date().getTime(), 
			                    time        : 0, 
			                    screenWidth : 0, 
			                    screenHeight: 0 };
 
			init();
			animate();
 
			function init() {
 
				vertex_shader = document.getElementById('vs').textContent;
				fragment_shader = document.getElementById('fs').textContent;
 

 
				canvas = document.querySelector( 'canvas' );

 
				// Initialise WebGL
 
				try {
 
					gl = canvas.getContext( 'experimental-webgl' );
 
				} catch( error ) { }
 
				if ( !gl ) {
 
					throw "cannot create webgl context";
 
				}
 
				// Create Vertex buffer (2 triangles)
 
				buffer = gl.createBuffer();
				gl.bindBuffer( gl.ARRAY_BUFFER, buffer );
				gl.bufferData( gl.ARRAY_BUFFER, new Float32Array( [ - 1.0, - 1.0, 1.0, - 1.0, - 1.0, 1.0, 1.0, - 1.0, 1.0, 1.0, - 1.0, 1.0 ] ), gl.STATIC_DRAW );
 
				// Create Program
 
				currentProgram = createProgram( vertex_shader, fragment_shader );
 
				onWindowResize();
				window.addEventListener( 'resize', onWindowResize, false );
 
			}
 
			function createProgram( vertex, fragment ) {
 
				var program = gl.createProgram();
 
				var vs = createShader( vertex, gl.VERTEX_SHADER );
				var fs = createShader( '#ifdef GL_ES\nprecision highp float;\n#endif\n\n' + fragment, gl.FRAGMENT_SHADER );
 
				if ( vs == null || fs == null ) return null;
 
				gl.attachShader( program, vs );
				gl.attachShader( program, fs );
 
				gl.deleteShader( vs );
				gl.deleteShader( fs );
 
				gl.linkProgram( program );
 
				if ( !gl.getProgramParameter( program, gl.LINK_STATUS ) ) {
 
					alert( "ERROR:\n" +
					"VALIDATE_STATUS: " + gl.getProgramParameter( program, gl.VALIDATE_STATUS ) + "\n" +
					"ERROR: " + gl.getError() + "\n\n" +
					"- Vertex Shader -\n" + vertex + "\n\n" +
					"- Fragment Shader -\n" + fragment );
 
					return null;
 
				}
 
				return program;
 
			}
 
			function createShader( src, type ) {
 
				var shader = gl.createShader( type );
 
				gl.shaderSource( shader, src );
				gl.compileShader( shader );
 
				if ( !gl.getShaderParameter( shader, gl.COMPILE_STATUS ) ) {
 
					alert( ( type == gl.VERTEX_SHADER ? "VERTEX" : "FRAGMENT" ) + " SHADER:\n" + gl.getShaderInfoLog( shader ) );
					return null;
 
				}
 
				return shader;
 
			}
 
			function onWindowResize( event ) {
 
				canvas.width = window.innerWidth;
				canvas.height = window.innerHeight;
 
				parameters.screenWidth = canvas.width;
				parameters.screenHeight = canvas.height;
 
				gl.viewport( 0, 0, canvas.width, canvas.height );
 
			}
 
			function animate() {
 
				requestAnimationFrame( animate );
				render();
 
			}
 
			function render() {
 
				if ( !currentProgram ) return;
 
				parameters.time = new Date().getTime() - parameters.start_time;
 
				gl.clear( gl.COLOR_BUFFER_BIT | gl.DEPTH_BUFFER_BIT );
 
				// Load program into GPU
 
				gl.useProgram( currentProgram );
 
				// Set values to program variables
 
				gl.uniform1f( gl.getUniformLocation( currentProgram, 'time' ), parameters.time / 1000 );
				gl.uniform2f( gl.getUniformLocation( currentProgram, 'resolution' ), parameters.screenWidth, parameters.screenHeight );
 
				// Render geometry
 
				gl.bindBuffer( gl.ARRAY_BUFFER, buffer );
				gl.vertexAttribPointer( vertex_position, 2, gl.FLOAT, false, 0, 0 );
				gl.enableVertexAttribArray( vertex_position );
				gl.drawArrays( gl.TRIANGLES, 0, 6 );
				gl.disableVertexAttribArray( vertex_position );
 
			}
 
		</script> 
 
	</body> 
</html>
